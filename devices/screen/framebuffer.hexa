// The Tofita Kernel
// Copyright (C) 2022-2023 Oleh Petrenko
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as published by
// the Free Software Foundation, version 3 of the License.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// Performs visualization onto the screen

// Speed of rendering mostly depends on cache-locality
// Remember: top-down, left-to-right: for(y) for(x) {}, not other way!

/// Native framebuffer
var _framebuffer: FrameBuffer = null
// TODO for some reason this compiles `var _framebuffer: FrameBuffer = null/` note `/` at the end

// Avoid one level of pointer indirection
// TODO make sure this in the huge/epic pages and 4k aligned
var _pixels: ArrayPointer<ByValue<Pixel32>> = null

/// Current screen
/// Allows to avoid pointer indirection
class Screen {
	static var width: UInt32 = 0
	static var widthMinusOne: UInt32 = 0
	static var height: UInt32 = 0
	static var heightMinusOne: UInt32 = 0
	static var pixels: ArrayPointer<ByValue<Pixel32>> = null
	/// Actual pixel buffer to be used
	// TODO just allocate memory instead of bitmap
	// TODO should be per-screen
	static var doublebuffer: Bitmap32 = null
}

fun allocateBitmap(width: UInt32, height: UInt32): Bitmap32 {
	// TODO allocateStruct<T>
	var result = PhysicalAllocator.allocateBytes(
		sizeOf<Bitmap32Interim>() +
		(sizeOf<Pixel32>() * width * height)
	) as! Bitmap32Interim
	result.width = width
	result.height = height
	result.pixels = (result as! UInt64 + 8 + 8) as! ArrayPointer<ByValue<Pixel32>>
	return result as! Bitmap32
}

fun setFramebuffer(framebuffer: FrameBuffer) {
	_framebuffer = framebuffer
	// TODO make clearer that this will be overwritten
	_pixels = _framebuffer.base as! ArrayPointer<ByValue<Pixel32>>
}

// Very fast, but not precise, alpha multiply
fun mul255(a255: UInt32, c255: UInt32): UInt8 {
	return ((a255 + 1u32) * c255 >> 8u32) as! UInt8
}

fun blend255(target: UInt8, color: UInt8, alpha: UInt8): UInt8 {
	return (mul255(alpha, color) + mul255(255u8 - alpha, target))
}

// TODO by val
fun blendPixel(x: Int64, y: Int64, pixel: Pixel32) {
	// TODO make all values like `width` global and overwrite them on FB switch
	// TODO taken ifs are faster?
	if (x > Screen.widthMinusOne) || (y > Screen.heightMinusOne) || (x < 0) || (y < 0) {
		return
	}

	let p = Screen.pixels[y * Screen.width + x].ref.rgba.ref
	let a = pixel.rgba.ref.a
	let aNegative = 255u32 - a

	p.r = mul255(a, pixel.rgba.ref.r) + mul255(aNegative, p.r)
	p.g = mul255(a, pixel.rgba.ref.g) + mul255(aNegative, p.g)
	p.b = mul255(a, pixel.rgba.ref.b) + mul255(aNegative, p.b)

	// TODO type check _pixels[y * _framebuffer.width + x] = p // Pixel32
}

fun setPixel(x: Int64, y: Int64, pixel: ByValue<Pixel32>) {
	// TODO Faster access `let width = s.w`?
	// TODO taken ifs are faster?
	if (x > Screen.widthMinusOne) || (y > Screen.heightMinusOne) || (x < 0) || (y < 0) {
		return
	}

	Screen.pixels[y * Screen.width + x] = pixel
}

fun drawBitmap32WithAlpha(bitmap: Bitmap32, x: Int16, y: Int16) {
	for yy in bitmap.height {
		for xx in bitmap.width {
			blendPixel(x + xx, y + yy, bitmap.pixels[yy * bitmap.width + xx].ref)
		}
	}
}

fun drawBitmap32(bitmap: Bitmap32, x: Int16, y: Int16) {
	for yy in bitmap.height {
		for xx in bitmap.width {
			setPixel(x + xx, y + yy, bitmap.pixels[yy * bitmap.width + xx])
		}
	}
}

fun drawRectangleWithAlpha(color: Pixel32, x: Int16, y: Int16, width: UInt16, height: UInt16) {
	for yy in height {
		for xx in width {
			blendPixel(x + xx, y + yy, color)
		}
	}
}

fun drawRectangle(color: ByValue<Pixel32>, x: Int16, y: Int16, width: UInt16, height: UInt16) {
	for yy in height {
		for xx in width {
			setPixel(x + xx, y + yy, color)
		}
	}
}

fun drawRectangleOutline(color: ByValue<Pixel32>, x: Int16, y: Int16, width: UInt16, height: UInt16) {
	for yy in height {
		for xx in width {
			// Rendering left and far right points sequentally should be
			// better for cache-locality than vertical lines
			// At least this is true for small rectangles (like buttons)
			if yy == 0 || xx == 0 || xx == width - 1 || yy == height - 1 {
				setPixel(x + xx, y + yy, color)
			}
		}
	}
}

fun line45smooth(color: ByValue<Pixel32>, x: Int16, y: Int16, width: Int16, mod: Int16) {
	color.ref.rgba.ref.a = 98
	var xx: Int16 = 0
	for xi in width - 1 {
		xx += mod
		setPixel(xx + x, y + xi, color)
		blendPixel(xx + x, y + xi + 1, color.ref)
		blendPixel(xx + x + mod, y + xi, color.ref)
	}
	xx += mod
	setPixel(xx + x, y + width - 1, color)
}

fun lineHorizontal(color: ByValue<Pixel32>, x: Int16, y: Int16, rightLength: UInt16) {
	for i in rightLength {
		setPixel(x + i, y, color)
	}
}

fun lineHorizontalWithAlpha(color: Pixel32, x: Int16, y: Int16, rightLength: UInt16) {
	for i in rightLength {
		blendPixel(x + i, y, color)
	}
}

fun lineVertical(color: ByValue<Pixel32>, x: Int16, y: Int16, bottomLength: UInt16) {
	for i in bottomLength {
		setPixel(x, y + i, color)
	}
}

fun lineVerticalWithAlpha(color: Pixel32, x: Int16, y: Int16, bottomLength: UInt16) {
	for i in bottomLength {
		blendPixel(x, y + i, color)
	}
}
