// The Tofita Kernel
// Copyright (C) 2022-2023 Oleh Petrenko
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as published by
// the Free Software Foundation, version 3 of the License.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

fun processInit(process TofitaProcess) {
	process.pml4 = newCR3(processes[0].pml4)
	process.schedulable = false // Not yet ready
	process.scheduleOnNextTick = false // Prevent DoS attack
	// TODO memset as template<> or get rid of memset altogether
	process.frame = new InterruptFrame()
	memset(process.frame as! ArrayPointer<UInt8>, 0, sizeOf<InterruptFrame>()) // Zeroing
	process.is64bit = true
	process.limits = AddressAwareness.Bit64limit8TB
	process.frame.cs = Selector.USER_CODE64_SEL_USER_MODE
	process.frame.ss = Selector.USER_DATA32_SEL_USER_MODE
	// TODO fs gs
	// process.frame.flags = 0x002 // No interrupts
	process.frame.flags = 0x202
	process.syscallToHandle = TofitaSyscalls.Noop
	process.messages = null
	// TODO init those fields in class declaration and constructor
	// new (kind ProcessKind.UserProcess)
	process.awaitsGetMessage = false
}

fun processCreate() TofitaProcess {
	var index UInt64 = 1 // Ignore Idle
	while index < processes.length {
		if processes[index] == null {
			processes[index] = new TofitaProcess()
		}

		if processes[index].present != true {
			break
		}

		index++
	}

	if index == processes.length {
		processes[index] = new TofitaProcess()
	}

	var process TofitaProcess = processes[index]
	process.pid = index
	process.present = true // Occupied

	return process
}

// Note: supposedly 10.000 system-wise
let messagesBufferSize UInt64 = 256 // TODO better idea

fun getOrCreateMessagesBuffer(@volatile process TofitaProcess) /*TODO wapi.*/[Msg]? {
	if process.messages == null {
		process.messages = []
		process.messagesUsed = 0

		// TODO this check is useless
		// TODO must error `if process.messages != physical_not_found {` comparison between pointer and integer
		/*
		if process.messages as! UInt64 != physical_not_found {
			//memset(process.messages as! ArrayPointer<UInt8>, 0, bytes)
			process.messagesUsed = 0
		} else {
			process.messages = null
			return null
		}
		*/
	}

	return process.messages
}

// TODO make all this TofitaProcess class methods + @final
fun postMessage(@volatile process TofitaProcess, payload PostMessagePayload) Bool {
	// TODO do not allocate messages queue if no windows and not awaits for them
	let messages = getOrCreateMessagesBuffer(process)
	if messages == null {
		return false
	}

	// TODO guard
	let messages = messages as! [Msg]

	if process.messagesUsed == messagesBufferSize {
		return false
	}

	var message Msg = messages[process.messagesUsed]

	if message == null {
		message = new Msg()
		messages[process.messagesUsed] = message
	}

	message.hwnd = payload.hWnd as! Handle
	message.message = payload.msg
	message.wParam = payload.wParam as! WParam
	message.lParam = payload.lParam as! LParam

	message.time = uptimeMilliseconds as! UInt32 // TODO `& 0xFF....` or better truncation method? `UInt64.truncate32() UInt32`?
	message.pt.ref.x = mouseX // TODO
	message.pt.ref.y = mouseY // TODO
	message.lPrivate = 0

	process.messagesUsed++

	// TODO describe the logic here
	if process.awaitsGetMessage {
		process.awaitsGetMessage = false
		process.syscallToHandle = TofitaSyscalls.GetMessage
	}

	return true
}

fun hasMessages(process TofitaProcess) Bool {
	if process.messagesUsed > 0 {
		return true
	}

	return false
}

fun getMessage(@volatile process TofitaProcess, payload GetMessagePayload) Bool {
	// Do not allocate until needed
	if process.messages == null {
		return false
	}

	var messages = getOrCreateMessagesBuffer(process)
	if messages == null {
		return false
	}

	// TODO guard
	let messages = messages as! [Msg]

	// TODO does not apply to wmQuit/wmPaint
	if process.messagesUsed == 0 {
		return false
	}

	// TODO filters & hWnd
	let message Msg = messages[0]
	let target = payload

	target.hwnd = message.hwnd as! UInt64
	target.message = message.message
	target.wParam = message.wParam as! UInt64
	target.lParam = message.lParam as! UInt64

	target.time = message.time
	target.pt.ref.x = message.pt.ref.x
	target.pt.ref.y = message.pt.ref.y
	target.lPrivate = message.lPrivate

	// Shift first element
	process.messagesUsed--

	for i in process.messagesUsed {
		messages[i] = messages[i + 1]
	}

	return true
}

fun processDestroy(@volatile process TofitaProcess) {
	// TODO de-allocate stuff like virtual memory table and it's physical memory
	// ^ in essence should call VirtualFree for all virtual blocks,
	// if user process would call them,
	// cause memory could be reused by other processes
	process.present = false
	process.messages = null

	var window = overlappedWindow_findAnyProcessWindow(process.pid)

	// Destroy every process' window
	while window != null {
		overlappedWindow_destroy(window.windowId)
		window = overlappedWindow_findAnyProcessWindow(process.pid)
	}
}
