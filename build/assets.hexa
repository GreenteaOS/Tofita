// The Tofita Kernel
// Copyright (C) 2021  Oleg Petrenko
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as published by
// the Free Software Foundation, version 3 of the License.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

declare class Config {
	let warnings: Bool
	let tempFolder: String
}

let dlls = [
	// Order is important
	'ntdll',
	'kernel32',
	'gdi32',
	'user32',
	'advapi32'
]

let deps: [[String]] = [
	[],
	['ntdll'],
	['ntdll', 'kernel32'],
	['ntdll', 'kernel32', 'gdi32'],
	['ntdll', 'kernel32']
]

fun packFonts(config: Config) {
		let fs = require('fs')
		let chars: [{code:Int, x:Int, y:Int, width:Float}] =
			JSON.parse(fs.readFileSync('assets/root/Windows/Fonts/default.json').toString().trim())

		let charSize = 2 + 2 + 8
		let size = 256 * charSize // Just ASCII for now
		let buffer = Buffer.alloc(size) // zeroed

		for ascii in 256 {
			for char in chars {
				if char.code == ascii {
					var offset = charSize * ascii
					offset = buffer.writeUInt16LE(char.x, offset)
					offset = buffer.writeUInt16LE(char.y, offset)
					offset = buffer.writeDoubleLE(char.width, offset)
				}
			}
		}

		fs.writeFileSync(config.tempFolder + '\\tofita\\ascii.tofita', buffer, "binary")
}

fun packAssets(config: Config) {

		let fs = require('fs')

		let assetName: [String] = []
		let assetPath: [String] = []

		assetName.push('tofita.gnu')
		assetPath.push(config.tempFolder + '\\tofita\\tofita.exe')

		assetName.push('trapeze.tofita')
		assetPath.push(config.tempFolder + '\\tofita\\trapeze.bin')

		for dll in dlls {
			// TODO \ not werks in js assetName.push(`C:\Windows\System32\` + dll + '.dll')
			assetName.push(`C:\\Windows\\System32\\` + dll + '.dll')
			assetPath.push(config.tempFolder + '\\tofita\\\(dll).64.dll')

			assetName.push(`C:\\Windows\\SysWOW64\\` + dll + '.dll')
			assetPath.push(config.tempFolder + '\\tofita\\\(dll).32.dll')
		}

		packFonts(config)
		assetName.push('ascii.tofita')
		assetPath.push(config.tempFolder + '\\tofita\\ascii.tofita')

		fun addFolderRecursive(path) {
			if (fs.existsSync(path)) {
				fs.readdirSync(path).forEach(fun(file, index) {
					let curPath = path + "/" + file
					if (fs.lstatSync(curPath).isDirectory()) {
						addFolderRecursive(curPath)
					} else {
						assetName.push(curPath.substr('assets/'.length))
						assetPath.push(curPath)
					}
				})
			}
		}

		addFolderRecursive('assets/root')
		addFolderRecursive('assets/desktop')

		// Compute RAM disk size
		var ramDiskSize = 0
		for asset in assetPath {
			ramDiskSize += fs.statSync(asset).size
		}

		ramDiskSize += 4 // UInt32 ramDiskChecksum
		ramDiskSize += 4 // UInt32 ramDiskSize
		ramDiskSize += 4 // UInt32 assetsCount
		ramDiskSize += ( //struct assetDefinition
			//{
			256 + //	var path[256]: UInt8
			4 + //	    UInt32 size
			4 //	    UInt32 offsetFromRamDiskFirstByte // NOT relative to `data`
			//}
		) * assetName.length
		//uint8_t* data

		// Round upto 4 bytes
		ramDiskSize = Math.ceil(ramDiskSize / 4) * 4
		console.log('ramDiskSize is', ramDiskSize, 'bytes')

		let buffer = Buffer.alloc(ramDiskSize) // zeroed
		buffer.writeUInt32LE(0, 0)
		buffer.writeUInt32LE(ramDiskSize,     4)
		buffer.writeUInt32LE(assetName.length,   8)
		let assetInfoSize = 256 + 4 + 4
		var assetInfoOffset = 4 + 4 + 4
		var assetDataOffset = ramDiskSize
		for i in assetName.length {
			let asset = assetName[i]
			let path = assetPath[i]
			let assetSize = fs.statSync(path).size
			buffer.write(asset, assetInfoOffset, "utf16le")
			buffer.writeUInt32LE(assetSize, assetInfoOffset + 256)

			let file = fs.readFileSync(path)
			assetDataOffset -= assetSize
			file.copy(buffer, assetDataOffset)

			buffer.writeUInt32LE(assetDataOffset, assetInfoOffset + 256 + 4)
			assetInfoOffset += assetInfoSize
		}

		fun getChecksum() {
			var checksum = 0
			// Start from 1, coz 0 contains original checksum
			var i = 1
			while i < Math.round(ramDiskSize / 4) {
				// Well, needs better checksum... JavaScript messes with numbers
				checksum = buffer.readUInt32LE(i * 4) & 0xFFFF ^ checksum
				i++
			}
			return checksum
		}

		let ramDiskChecksum = getChecksum()
		buffer.writeUInt32LE(ramDiskChecksum, 0)
		console.log('ramDiskChecksum is', ramDiskChecksum)

		fs.writeFileSync(config.tempFolder + '\\tofita\\iso\\TOFITA.DAT', buffer, "binary")
}
